import("//build/buildflag_header.gni")
import("//build/config/chrome_build.gni")
import("//build/config/compiler/compiler.gni")
import("//build/config/features.gni")
import("//extensions/buildflags/buildflags.gni")
import("//ppapi/buildflags/buildflags.gni")
import("//printing/buildflags/buildflags.gni")

source_set("browser") {
  configs += [
    "//electron/build:electron_config",
  ]

  sources = [
    # "api"
    "guest_view/tab_view/tab_view_guest.h",
    "guest_view/tab_view/tab_view_guest.cc",
    "guest_view/brave_guest_view_manager_delegate.h",
    "guest_view/brave_guest_view_manager_delegate.cc",
    "notifications/platform_notification_service_impl.h",
    "notifications/platform_notification_service_impl.cc",
    "brave_browser_context.h",
    "brave_browser_context.cc",
    "brave_content_browser_client.h",
    "brave_content_browser_client.cc",
    "brave_javascript_dialog_manager.h",
    "brave_javascript_dialog_manager.cc",
    "brave_permission_manager.h",
    "brave_permission_manager.cc",
    "importer/brave_external_process_importer_host.cc",
    "importer/brave_external_process_importer_host.h",
    "password_manager/brave_credentials_filter.h",
    "password_manager/brave_credentials_filter.cc",
    "password_manager/brave_password_manager_client.h",
    "password_manager/brave_password_manager_client.cc",
    "renderer_preferences_helper.h",
    "renderer_preferences_helper.cc",
    "renderer_host/brave_render_message_filter.h",
    "renderer_host/brave_render_message_filter.cc",
  ]

  public_deps = [
    "//components/autofill/core/browser",
  ]

  deps = [
    ":apis",
    ":proxy",
    ":tor",
    "//electron/atom/browser",
    "//electron:common",
    "//electron/muon/app",
    "//electron/muon/browser",
    "//chrome/common",
    "//components/autofill/content/browser:risk_proto",
    "//components/autofill/content/browser",
    "//components/autofill/core/browser",
    "//components/resources",
    "//components/spellcheck:buildflags",
    "//components/subresource_filter/content/browser",
    "//components/subresource_filter/core/browser",
    "//components/sync_preferences",
    "//components/ukm/content",
    "//components/visitedlink/browser",
    "//components/visitedlink/common",
    "//components/webdata_services",
    "//content/public/common:service_names",
    "//device/geolocation",
    "//device/usb/mojo",
    "//device/usb/public/mojom",
    "//gpu/config",
    "//mojo/edk",
    "//mojo/public/cpp/bindings",
    "//services/device/public/mojom",
    "//services/identity:lib",
    "//third_party/blink/public:image_resources",
    "//third_party/blink/public:resources",
  ]

  if (is_linux) {
    deps += [
      "//components/crash/content/app",
      "//components/crash/content/browser",
    ]
  }

  if (enable_basic_printing || enable_print_preview) {
    deps += [
      "//components/printing/browser",
      "//printing",
    ]
  }

  if (enable_plugins) {
    sources += [
      "plugins/brave_plugin_service_filter.cc",
      "plugins/brave_plugin_service_filter.h",
    ]
  }

  if (enable_extensions) {
    public_deps += [
      "extensions",
    ]

    deps += [
      "//chrome/common/extensions/api",
      "//chrome/common/extensions/api:api_registration",
      "//chrome/common/extensions/api:extensions_features",
      "//extensions/browser",
    ]
  }
}

source_set("tab_manager") {
  configs += [
    "//electron/build:electron_config",
  ]

  sources = [
    "resource_coordinator/guest_tab_manager.cc",
    "resource_coordinator/guest_tab_manager.h",
  ]

  deps = [
    "//content/public/common",
    "//electron/chromium_src:tab_manager"
  ]
}

source_set("apis") {
  configs += [
    "//electron/build:electron_config",
  ]

  sources = [
    "api/brave_api_component_updater.cc",
    "api/brave_api_component_updater.h",
    "api/brave_api_extension.cc",
    "api/brave_api_extension.h",
    "api/navigation_controller.cc",
    "api/navigation_controller.h",
    "api/navigation_handle.cc",
    "api/navigation_handle.h",
    "ui/brave_tab_strip_model_delegate.cc",
  ]

  deps = [
    "//electron/build/node",
    "//electron/muon/browser",
    "//v8:v8",
    "//v8:v8_libplatform",
    "//third_party/blink/public:blink_headers",
  ]

  public_deps = [
    "component_updater",
  ]
}

source_set("proxy") {
  configs += [
    "//electron/build:electron_config",
  ]

  sources = [
    "net/proxy_resolution/proxy_config_service_tor.cc",
    "net/proxy_resolution/proxy_config_service_tor.h",
    "net/tor_proxy_network_delegate.cc",
    "net/tor_proxy_network_delegate.h",
  ]

  deps = [
    "//net",
    "//third_party/blink/public:blink_headers",
  ]
}

source_set("tor") {
  configs += [
    "//electron/build:electron_config",
  ]

  sources = [
    "tor/tor_launcher_factory.cc",
    "tor/tor_launcher_factory.h",
  ]

  deps = [
   "//third_party/blink/public:blink_headers",
  ]
}
